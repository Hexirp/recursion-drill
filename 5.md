# 再帰ドリル(5)：自然数に対する少し複雑な再帰

今回学ぶ再帰は少し複雑だが、すべてループと同等である。

##最大公約数

最大公約数(GCD: greatest common divisor)は、中学校で習っただろう。そのアルゴリズムの要は、共通因数を見つけることである。共通因数を見つけるには、素因数分解しないといけない。素因数分解のためには、素数表を作成する必要がある。大きな数が素数であるか判定するのは、かなり難しいし時間がかかる。よって、このアルゴリズムは現実的ではない。

最大公約数を算出するには、実はとても簡単な方法がある。なぜ中学校でこちらを教えないか不思議なくらいだ。2 以上大きい自然数 a と b があって、a >= b とすると、最大公約数は、a - b で計算できる。ウソだと思うかもしれないので、実際に試してみよう。12 と 8 の最大公約数は 4 である。12 から 8 を引くと 4 になるから、見事に最大公約数が求まっている。

その種明かしをしよう。a と b の最大公約数を G とし、a = G * A、b = G * B とおく。a - b = G (A - B) なので、引き算しても G は消えない。A - B の部分を 1 にできれば、G が求まるというわけだ。これを再帰で実装すると以下のようになる。

```haskell
gcdSlow :: Int -> Int -> Int
gcdSlow a 0 = a
gcdSlow a b
  | c >= b    = gcdSlow c b
  | otherwise = gcdSlow b c
  where
    c = a - b
```

gcdSlow は a >= b と仮定していることに注意。なぜ基底部がこうなるのかは自分で考えること。

###演習

gcdSlow は何度も引き算をして、c >= b のときにはじめて引数をひっくり返した。何度も引いて、最後に小さくなるものとは、余りに他ならない。よって、余りを計算するようにすれば、この関数は劇的に速くなる。

余りを計算する演算子 mod を使って、gcdSlow を書き直し、gcdFast として定義せよ。

```haskell
gcdFast :: Int -> Int -> Int
gcdFast = undefined
```

###演習

最小公倍数(LCM: least common multiple) を求める関数 lcmFast を gcdFast を使って実装せよ。ただし、引数が任意の順序でよいように工夫せよ。

```haskell
lcmFast :: Int -> Int -> Int
lcmFast = undefined
```

##累乗

累乗を計算する関数 power を思い出そう。

```haskell
power m 1 = m
power m n = power m (n - 1) * m
```

この実装を n が 0 のときにも対応できるように、以下のように書き直す。

```haskell
power :: Int -> Int -> Int
power _ 0 = 1
power m n = power m (n - 1) * m
```

###演習

実は累乗をさらに高速に計算するアルゴリズムが存在する。指数が 2 の累乗であれば、倍、倍、倍、と計算していけることを利用する。たとえば、2^11 を考えよう、これは 2^1 * 2^2 * 2^8 である。よって、ビットが立っていれば、そのときの m^x (ただし x は 2 の累乗)を掛ければよい。

以下の undefined の部分を書き換えて、高速な累乗計算を実装せよ。

```hakell
powerFast :: Int -> Int -> Int
powerFast _ 0 = 1
powerFast m n
  | odd n     = powerFast undefined undefined * m
  | otherwise = powerFast undefined undefined
```

ヒント：n を 2 進数表記で表した場合、odd は最も右の桁のビットが立っているかを調べている。

###演習

累乗を計算する power の末尾再帰版を powerIter とする。

```haskell
powerIter :: Int -> Int -> Int
powerIter m n = powerIter' m n 1

powerIter' :: Int -> Int -> Int -> Int
powerIter' _ 0 acc = acc
powerIter' m n acc = powerIter' m (n - 1) (acc * m)
```

この実装に対しても高速版を実装せよ。

```haskell
powerFastIter :: Int -> Int -> Int
powerFastIter m n = powerFastIter' m n 1

powerFastIter' :: Int -> Int -> Int -> Int
powerFastIter' _ 0 acc = acc
powerFastIter' m n acc
  | odd n     = powerFastIter' undefined undefined undefined
  | otherwise = powerFastIter' undefined undefined undefined
```

##フィボナッチ数列

Haskell では、フィボナッチ数列をあたかも数学の定義のように実装できる。

```haskell
fib :: Int -> Integer
fib 0 = 0
fib 1 = 1
fib n = fib (n - 2) + fib (n - 1)
```

このように、自分自身を複数回呼び出す再帰を「多重再帰」という。

###演習

fib の末尾再帰版 fibFast を実装せよ。

```haskell
fibFast :: Int -> Integer
fibFast n = fibFast' n 0 1

fibFast' :: Int -> Integer -> Integer -> Integer
fibFast' 0 x _ = x
fibFast' n x y = fibFast' undefined undefined undefined
```

蓄積変数が2つあることに注意。

##偶数奇数

偶数か奇数かを調べる関数 isEven と isOdd を思い出そう。

```haskell
isEven 0 = True
isEven 1 = False
isEven n = isEven (n - 2)

isOdd :: Int -> Bool
isOdd 0 = False
isOdd 1 = True
isOdd n = isOdd (n - 2)
```

これは、以下のように互いに呼び合うように変更できる。

```haskell
isEven :: Int -> Bool
isEven 0 = True
isEven n = isOdd (n - 1)

isOdd :: Int -> Bool
isOdd 0 = False
isOdd n = isEven (n - 1)
```

このような再帰を「相互再帰」という。

###演習

isOdd の基底部も True を返すよう実装できるだろうか？

```haskell
isEven :: Int -> Bool
isEven 0 = True
isEven n = undefined

isOdd :: Int -> Bool
isOdd 1 = True
isOdd n = undefined
```
