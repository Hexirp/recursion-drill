# 再帰ドリル(7)：メモ化

フィボナッチ数列を素朴な再帰で実装したコードを思い出そう。

```haskell
fib :: Integer -> Integer
fib 0 = 0
fib 1 = 1
fib n = fib (n - 2) + fib (n - 1)
```

このコードは美しいが、実際に動かしてみると耐えられないくらい遅いことが分かる。

```haskell
> :set +s
> fib 33
3524578
(9.52 secs, 935623016 bytes)
```

TBD 遅い理由：

TBD 図：

これまで学んだように、フィボナッチ数列は簡単に末尾再帰の形に変更できる。

```haskell
fibFast :: Integer -> Integer
fibFast n = fibFast' n 0 1

fibFast' :: Integer -> Integer -> Integer -> Integer
fibFast' 0 x _ = x
fibFast' n x y = fibFast' (n - 1) y (x + y)
```

そのコードは無駄な再計算をしないので速い。

```haskell
> fibFast 33
3524578
(0.01 secs, 2115668 bytes)
```

残念ながら、あらゆる再帰が末尾再帰に簡単に直せる訳ではないし、簡単に高速化できる訳でもない。

再帰の形を変えずに高速化する常套手段はメモ化である。メモ化とは、計算した結果を辞書に登録してキャッシュし、再計算を防ぐことである。動的計画法と呼ばれることもある。

辞書は関数の外側に持たなければならない。副作用を簡単に扱えるプログラミング言語では、これは簡単である。

##Haskellでのメモ化

ここからは Haskell 固有の話なので、興味がない方は読まなくても構わない。

Haskell では、純粋な実装を目指したくなるので、辞書を引数として持ち回せばいいと思うかも知れないが、これはうまくいかない。(理由が分からない人は、実際に実装して確かめながら理由を考えて欲しい。)

結局、副作用を用いてメモ化を実装すると、たとえば以下のようなコードになる。

```haskell
import Data.IORef
import Data.Map (Map)
import qualified Data.Map as M

memoFib :: Integer -> IO Integer
memoFib n = do
    ref <- newIORef $ M.fromList [(0,0),(1,1)]
    memoFib' n ref
    
memoFib' :: Integer -> IORef (Map Integer Integer) -> IO Integer
memoFib' n ref = do
    memo <- readIORef ref
    case M.lookup n memo of
        Just z  -> return z
        Nothing -> do
            x <- memoFib' (n - 2) ref
            y <- memoFib' (n - 1) ref
            let z = x + y
            modifyIORef ref (M.insert n z)
            return z
```

実際に動かして、メモ化の効果を見てみよう。

```haskell
> memoFib 33
3524578
(0.03 secs, 2954884 bytes)
```

memoFib の返り値が IO Integer になっているのは、Haskeller としてはくやしい。そこで、禁断の unsafePerformIO を使いたくなる。

```haskell
import Data.IORef
import Data.Map (Map)
import qualified Data.Map as M
import System.IO.Unsafe

memoFib :: Integer -> Integer
memoFib n = unsafePerformIO $ do
    ref <- newIORef $ M.fromList [(0,0),(1,1)]
    memoFib' n ref
    
memoFib' :: Integer -> IORef (Map Integer Integer) -> IO Integer
memoFib' n ref = do
    memo <- readIORef ref
    case M.lookup n memo of
        Just z  -> return z
        Nothing -> do
            x <- memoFib' (n - 2) ref
            y <- memoFib' (n - 1) ref
            let z = x + y
            modifyIORef ref (M.insert n z)
            return z
```

ただ、unsafePerformIO を使うと後ろ髪を引かれる思いになることは確かだろう。

## memoize パッケージの memoFix

幸いにも、純粋にメモ化する方法が知られている。それには、memoize パッケージで定義されている memoFix を使う。(fix とは「不動点コンビネータ」という意味であるが、それが何かを知る必要はない。)

まず、memoize パッケージをインストールしていただきたい。

    % cabal install memoize

memoFix を使うには、以下のようにする。

1. 高速化したい再帰関数の引数を一つ増やす。この引数には自分自身が渡ってくる。再帰には、この引数を使う。
2. 1. で定義した関数を memoFix (または memoFix2 など)に渡すと、メモ化されたラッパー関数が完成する

fib のコードには、以下のようにメモ化の実装に変更できる。

```haskell
import Data.Function.Memoize

memoFib :: Integer -> Integer
memoFib n = memoFix fibF n
    
fibF :: (Integer -> Integer) -> (Integer -> Integer)
fibF _ 0 = 0
fibF _ 1 = 1
fibF f n = f (n - 2) + f (n - 1)
```

実際に動かしてメモ化の効果を確かめてみよう。

```haskell
> memoFib 33
3524578
(0.11 secs, 2224880 bytes)
```

##カタラン数

以下が経路の総数を計算するコードであった。

```haskell
catalan :: Integer -> Integer
catalan n = cat n n

cat :: Integer -> Integer -> Integer
cat _ 0 = 1
cat m n
  | m == n    = cat m (n - 1)
  | otherwise = cat m (n - 1) + cat (m - 1) n
```

これをメモ化を使って高速にせよ。

```haskell
memoCatalan :: Integer -> Integer
memoCatalan = undefined

catF :: (Integer -> Integer -> Integer) -> (Integer -> Integer -> Integer)
catF = undefined
```

以下が二分木の総数を計算するコードであった。

```haskell
catalan2 :: Integer -> Integer
catalan2 0 = 1
catalan2 n = sum (zipWith (*) xs ys)
  where
    xs = map catalan2 [0 .. n - 1]
    ys = map catalan2 [n - 1, n - 2 .. 0]
```

これをメモ化を使って高速にせよ。

```haskell
memoCatalan2 :: Integer -> Integer
memoCatalan2 n = undefined

catalanF2 :: (Integer -> Integer) -> (Integer -> Integer)
catalanF2 = undefined
```

##コインの両替

以下がコインの両替を解くコードであった。

```haskell
coin :: Integer -> [Integer] -> Integer
coin 0 _      = 1
coin _ []     = 0
coin n ccs@(c:cs)
  | n < 0     = 0
  | otherwise = coin n cs + coin (n - c) ccs
```

これをメモ化を使って高速にせよ。

```haskell
memoCoin :: Integer -> [Integer] -> Integer
memoCoin = undefined

coinF :: (Integer -> [Integer] -> Integer) -> (Integer -> [Integer] -> Integer)
coinF = undefined
```

[[目次]](README.md) [[演習7]](7.hs)
