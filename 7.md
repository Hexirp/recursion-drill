# 再帰ドリル(7)：メモ化

フィボナッチ数列を素朴な再帰で実装したコードを思い出そう。

```haskell
my_fib :: Integer -> Integer
my_fib 0 = 0
my_fib 1 = 1
my_fib n = my_fib (n - 2) + my_fib (n - 1)
```

このコードは美しいが、実際に動かしてみると耐えられないくらい遅いことが分かる。

```haskell
> :set +s
> my_fib 33
3524578
(9.52 secs, 935623016 bytes)
```

遅い理由は、下の図のように同じ計算を何度も繰り返すからだ。

![フィボナッチ数列の樹状計算](figs/fib-tree.png?raw=true)

これまで学んだように、フィボナッチ数列は簡単に末尾再帰の形に変更できる。

```haskell
my_fib_iter :: Integer -> Integer
my_fib_iter a = iter a 0 1
  where
    iter :: Integer -> Integer -> Integer -> Integer
    iter 0 x _ = x
    iter n x y = iter (n - 1) y (x + y)
```

そのコードは無駄な再計算をしないので速い。

```haskell
> my_fib_iter 33
3524578
(0.01 secs, 2115668 bytes)
```

残念ながら、あらゆる再帰が末尾再帰に簡単に直せる訳ではないし、簡単に高速化できる訳でもない。

再帰の形を変えずに高速化する常套手段はメモ化である。メモ化とは、計算した結果を辞書に登録してキャッシュし、再計算を防ぐことである。動的計画法と呼ばれることもある。

辞書は関数の外側に持たなければならない。副作用を簡単に扱えるプログラミング言語では、これは簡単である。

##Haskellでのメモ化

ここからは Haskell 固有の話なので、興味がない方は読まなくても構わない。

Haskell では、純粋な実装を目指したくなる。そのためには、辞書を引数として持ち回せばいいと思うかも知れないが、これはうまくいかない。(理由が分からない人は、実際に実装して確かめながら理由を考えて欲しい。)

結局、副作用を用いてメモ化を実装すると、たとえば以下のようなコードになる。

```haskell
import Data.IORef
import Data.Map (Map)
import qualified Data.Map as M

my_fib_io :: Integer -> IO Integer
my_fib_io a = do
    ref <- newIORef $ M.fromList [(0,0),(1,1)]
    fib a ref
  where
    fib :: Integer -> IORef (Map Integer Integer) -> IO Integer
    fib n ref = do
        memo <- readIORef ref
        case M.lookup n memo of
            Just z  -> return z
            Nothing -> do
                x <- fib (n - 2) ref
                y <- fib (n - 1) ref
                let z = x + y
                modifyIORef ref (M.insert n z)
                return z
```

実際に動かして、メモ化の効果を見てみよう。

```haskell
> my_fib_io 33
3524578
(0.03 secs, 2954884 bytes)
```

memoFib の返り値が IO Integer であるのは、Haskeller としてはくやしい。そこで、禁断の unsafePerformIO を使いたくなる。

```haskell
import Data.IORef
import Data.Map (Map)
import qualified Data.Map as M
import System.IO.Unsafe

my_fib_unsafe :: Integer -> Integer
my_fib_unsafe a = unsafePerformIO $ do
    ref <- newIORef $ M.fromList [(0,0),(1,1)]
    fib a ref
  where
    fib :: Integer -> IORef (Map Integer Integer) -> IO Integer
    fib n ref = do
        memo <- readIORef ref
        case M.lookup n memo of
            Just z  -> return z
            Nothing -> do
                x <- fib (n - 2) ref
                y <- fib (n - 1) ref
                let z = x + y
                modifyIORef ref (M.insert n z)
                return z
```

ただ、unsafePerformIO を使うと後ろ髪を引かれる思いになることは確かだろう。

## memoize パッケージの memoFix

幸いにも、純粋にメモ化する方法が知られている。深い説明はこのドリルの範囲を超えるので、今回は単に memoize パッケージで定義されている memoFix を使おう。(fix とは「不動点コンビネータ」という意味であるが、それが何かを知る必要はない。)

まず、memoize パッケージをインストールしていただきたい。

    % cabal install memoize

memoFix を使うには、以下のようにする。

1. 高速化したい再帰関数の引数を一つ増やす。この引数には自分自身が渡されてくる。再帰には、この引数を使う。
2. 1. で定義した関数を memoFix に渡すと、メモ化されたラッパー関数が完成する

fib のコードには、以下のようにメモ化の実装に変更できる。

```haskell
import Data.Function.Memoize

my_fib_memo :: Integer -> Integer
my_fib_memo x = memoFix fibF x
  where
    fibF :: (Integer -> Integer) -> (Integer -> Integer)
    fibF _ 0 = 0
    fibF _ 1 = 1
    fibF f n = f (n - 2) + f (n - 1)
```

実際に動かしてメモ化の効果を確かめてみよう。

```haskell
> my_fib_memo 33
3524578
(0.11 secs, 2224880 bytes)
```

##演習：カタラン数

以下が経路の総数を計算するコードであった。

```haskell
my_catalan :: Integer -> Integer
my_catalan x = cat x x
  where
    cat :: Integer -> Integer -> Integer
    cat _ 0 = 1
    cat m n
      | m == n    = cat m (n - 1)
      | otherwise = cat m (n - 1) + cat (m - 1) n
```

これをメモ化を使って高速にせよ。

```haskell
my_catalan_memo :: Integer -> Integer
my_catalan_memo x = undefined
  where
    catF :: (Integer -> Integer -> Integer) -> (Integer -> Integer -> Integer)
    catF = undefined
```

以下が二分木の総数を計算するコードであった。

```haskell
my_catalan2 :: Integer -> Integer
my_catalan2 0 = 1
my_catalan2 n = sum (zipWith (*) xs ys)
  where
    xs = map my_catalan2 [0 .. n - 1]
    ys = map my_catalan2 [n - 1, n - 2 .. 0]
```

これをメモ化を使って高速にせよ。

```haskell
my_catalan2_memo :: Integer -> Integer
my_catalan2_memo x = undefined
  where
    catF :: (Integer -> Integer) -> (Integer -> Integer)
    catF = undefined
```

##演習：コインの両替

以下がコインの両替を解くコードであった。

```haskell
my_coin :: Integer -> [Integer] -> Integer
my_coin 0 _   = 1
my_coin _ []  = 0
my_coin n ccs@(c:cs)
  | n < 0     = 0
  | otherwise = my_coin n cs + my_coin (n - c) ccs
```

memoFix が取る関数は一引数なので、そのように変更する。

```haskell
my_coin :: (Integer,[Integer]) -> Integer
my_coin (0,_)   = 1
my_coin (_,[])  = 0
my_coin (n,ccs@(c:cs))
  | n < 0     = 0
  | otherwise = my_coin (n,cs) + my_coin ((n - c),ccs)
```

これをメモ化を使って高速にせよ。

```haskell
my_coin_memo :: Integer -> [Integer] -> Integer
my_coin_memo x xs = memoFix coinF (x,xs)
  where
    coinF :: ((Integer,[Integer]) -> Integer) -> ((Integer,[Integer]) -> Integer)
    coinF = undefined
```

[[目次]](README.md) [[演習7]](7.hs)
