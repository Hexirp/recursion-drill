# 再帰ドリル(4)：再帰的な自然数

これまで自然数に対する再帰を学んできたが、Haskell が提供する基本的な関数/二項演算子を使ったために、なんだかよくわらかない部分もあったかもしれない。今回は、自然数の定義から始め、基本的な四則演算子を再実装する。

自然数を再帰関数で処理するのは、自然数が再帰的に定義されたデータだからである。このドリルがバカらしいと感じる人は、結局再帰を理解できない可能性があるので、あなどらないでいただきたい。

##自然数の定義

自然数を以下のように定義する。ただし、自然数は0から始まるとする。

```haskell
data Nat = Z | S Nat
```

数値と対応づけると以下のようになる

* 0 は Z
* 1 は S Z
* 2 は S (S Z)
* 3 は S (S (S Z))

最低限必要な定数/関数は、以下の通り。

* 0
* 0 か確かめる
* 1 を足す
* 1 を引く

これらを以下のように定義する。

```haskell
zero :: Nat
zero = Z

isZero :: Nat -> Bool
isZero Z = True
isZero _ = False

plus1 :: Nat -> Nat
plus1 = S

minus1 :: Nat -> Nat
minus1 (S n) = n
minus1 _     = error "minus1"
```

また、Int と Nat の変換関数を以下のように定義する。

```haskell
toInt :: Nat -> Int
toInt n = toInt' n 0
  where
    toInt' m acc
      | isZero m  = acc
      | otherwise = toInt' (minus1 m) (acc + 1)

fromInt :: Int -> Nat
fromInt 0 = Z
fromInt n = S (fromInt (n - 1))
```

これ以降、Nat を抽象データ型として扱うので、直接 Z や S を用いることはできない。[Nat ライブラリの全体像はここにある](Nat.hs)。

使ってみよう。

```haskell
% ghci Nat.hs 
> toInt $ plus1 zero
1
> toInt $ minus1 $ fromInt 3
2
```

##足し算

これまでに学んだように、Int に対する足し算を素朴な再帰で実装すると以下のようになる。

```haskell
plus :: Int -> Int -> Int
plus m 0 = m
plus m n = plus m (n - 1) + 1
```

これを参考に Nat に対する足し算を実装すると、以下のようになる。

```haskell
plus :: Nat -> Nat -> Nat
plus m n
  | isZero n  = m
  | otherwise = plus1 (m `plus` minus1 n)
```

##掛け算

これまでに学んだように、Int に対する掛け算を素朴な再帰で実装すると以下のようになる。

```haskell
mul :: Int -> Int -> Int
mul m 1 = m
mul m n = mul m (n - 1) + m
```

これを参考に Nat に対する掛け算を実装すると、以下のようになる。

```haskell
isOne :: Nat -> Bool
isOne n
  | isZero n          = False
  | isZero (minus1 n) = True
  | otherwise         = False

mul :: Nat -> Nat -> Nat
mul m n
  | isOne n   = m
  | otherwise = mul m (minus1 n) `plus` m
```

##演習

* Nat に対して引き算の関数 minus を定義しなさい
* Nat に対して割り算の関数 divide を定義しなさい (ヒント：まず lt を実装すること)
* Nat に対して余りを計算する関数 remainder を実装しなさい

